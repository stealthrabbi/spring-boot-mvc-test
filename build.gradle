plugins {
    id 'org.springframework.boot' version '1.5.2.RELEASE'
    id 'com.craigburke.client-dependencies' version '1.4.0'
}

group 'spring-boot-mvc-test'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

// analysis stuff
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

// Note: to run spring boot app, just type `./gradlew bootRun`, and browse http://localhost:8080/
// to build and test, run ./gradlew build
// clean, run ./gradelw clean

repositories {
    mavenCentral()
}

// demonstrating using the client-dependencies plugin for using npm to get some JS libraries
clientDependencies {
    installDir = 'src/main/webapp/lib'
    npm {
        'angular'('1.5.8')
        'bootstrap'('3.3.6')
        'restangular'('1.5.1')
        'angular-animate'('1.5.0', into: 'angular/modules')
        'animate.css'('1.0.0', url:'daneden/animate.css')
        'angular-ui-bootstrap'('1.3.x', from:'dist') {
            include 'ui-bootstrap-tpls.js'
        }
        'ag-grid'('8.*')
    }
}

war {
    baseName = 'MyStudentTestApp'
    version =  '1.0.0'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

checkstyle {
    toolVersion "7.5.1"
    ignoreFailures false
    maxErrors = 0
    maxWarnings = 0
}

dependencies {

    // spring-boot contains many spring components, such as core spring, spring mvc, spring test
    // jackson for JSON testing, etc.
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.4'


    // the default spring-boot-starter (embedded web container) is Tomcat. If you want to run in an
    // embedded Jetty server, add this depdency below. Other web starters exist as well.
//    providedRuntime group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: '1.4.3.RELEASE'

    // use spring-boot-test. no need to use 'spring-test'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}
